//Constants.
//Effect size: 272 bytes.
OUTER_SET EFFECT_SIZE = 0x110

//Array of effect offsets.
ACTION_DEFINE_ASSOCIATIVE_ARRAY EFFECT_OFFSETS BEGIN
    "opcode" => 0x10
    "target" => 0x14
    "power" => 0x18
    "parameter1" => 0x1c
    "parameter2" => 0x20
    "timing" => 0x24
    "duration" => 0x28
    "probability1" => 0x2c
    "probability2" => 0x2e
    "resource" => 0x30
    "dicenumber" => 0x38
    "dicesize" => 0x3c
    "savingthrow" => 0x40
    "savebonus" => 0x44
    "special" => 0x48
    "school" => 0x4c
    "resist_dispel" => 0x5c
    "parameter3" => 0x60
    "parameter4" => 0x64
    "resource2" => 0x70
    "resource3" => 0x78
    "projectile" => 0xa0
    "sectype" => 0xd0
END

//Patch functions.
DEFINE_PATCH_FUNCTION GET_EFFECT_FIELD STR_VAR field = "" RET value BEGIN
    field_offset = $EFFECT_OFFSETS("%field%")
    //Match on field to decide which read function to use.
    PATCH_MATCH "%field%" WITH
        "opcode"
        "target"
        "power"
        "parameter1"
        "parameter2"
        "duration"
        "dicenumber"
        "dicesize"
        "savingthrow"
        "savebonus"
        "special"
        "school"
        "resist_dispel"
        "parameter3"
        "parameter4"
        "projectile"
        "sectype" BEGIN
            READ_LONG field_offset value
        END
        "timing" "probability1" "probability2" BEGIN
            READ_SHORT field_offset value
        END
        //Order is important: resource will shadow resource2 and resource3.
        "resource2" "resource3" "resource" BEGIN
            READ_ASCII field_offset value (8) NULL
        END
        DEFAULT
            PATCH_FAIL "Field '%field%' is not a valid effect field."
    END
END

DEFINE_PATCH_FUNCTION SET_EFFECT_FIELD STR_VAR field = "" value = "" BEGIN
    field_offset = $EFFECT_OFFSETS("%field%")
    //Match on field to decide which write function to use.
    PATCH_MATCH "%field%" WITH
        "opcode"
        "target"
        "power"
        "parameter1"
        "parameter2"
        "duration"
        "dicenumber"
        "dicesize"
        "savingthrow"
        "savebonus"
        "special"
        "school"
        "resist_dispel"
        "parameter3"
        "parameter4"
        "projectile"
        "sectype" BEGIN
            WRITE_LONG field_offset value
        END
        "timing" "probability1" "probability2" BEGIN
            WRITE_SHORT field_offset value
        END
        //Order is important: resource will shadow resource2 and resource3.
        "resource2" "resource3" "resource" BEGIN
            WRITE_EVALUATED_ASCII field_offset ~%value%~ #8
        END
        DEFAULT
            PATCH_FAIL "Field '%field%' is not a valid effect field."
    END
END
